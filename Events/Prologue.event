//Made by Camtech075 of SerenesForest
//Modified by Nintenlord

EventPointerTable(0x7, MyEvents)

MyEvents:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno Dunno Dunno
POIN Tutorial
POIN TrapData TrapData
POIN Units Units //second one can be UnitsHard
POIN $0 $0 $0 $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
TurnEventPlayer(0x00,SpawnThief,2)
END_MAIN

ALIGN 32 //For some reason, CharacterBasedEvents have to be 32-Aligned?
CharacterBasedEvents:
SupportConvo(CSupport,Ruby,Franz,Ch1Village)
END_MAIN

LocationBasedEvents: //really more "map objects" such as shops/ houses/ etc.
Village(0xA,UpperLeftVillage,5,1)
House(0x00,UpperLeftHouse,0,7)
Chest(BagOfHolding,16,6)
END_MAIN


MiscBasedEvents:
AREA 0xB ChangeThiefAI [16,6] [16,6]
CauseGameOverIfLordDies //set flag 101/0x65 for other game overs
DefeatAll(EndingScene)
END_MAIN

ChangeThiefAI:
CHECK_COORDS (-1) 
SADD rB rC r0 
SetAI(16,6,0x06, 0x0C)
ENDA

Dunno:
WORD $00
END_MAIN

Tutorial:
WORD $00
END_MAIN

TrapData:

ENDTRAP

ALIGN 4

Units:
UNIT Romi EirikaLord Eirika Level(1,Ally,False) [3,13] 0x0 0x0 0x0 0x1 [Dealbreaker,Vulnerary] NoAI
UNIT Ruby LanceKnight Eirika Level(1,Ally,False) [2,12] 0x0 0x0 0x0 0x1 [IronLance,Vulnerary] NoAI
UNIT Ella ThiefF Eirika Level(1,Ally,False) [1,12] 0x0 0x0 0x0 0x1 [IronSword,Lockpick] NoAI
UNIT Ursula Cleric Eirika Level(1,Ally,False) [2,13] 0x0 0x0 0x0 0x1 [Heal,Vulnerary] NoAI
UNIT Rayleigh Archer Eirika Level(1,Ally,False) [3,12] 0x0 0x0 0x0 0x1 [IronBow] NoAI
UNIT Kilvonso Monk Eirika Level(1,Ally,False) [3,14] 0x0 0x0 0x0 0x1 [Lightning] NoAI
UNIT

EnemyUnits:
UNIT 0x80 Soldier 0x00 Level(1, Enemy, 1) [6,17] 0x00 0x00 0x0 0x00 [IronLance] MoveToAttackAI
UNIT 0x80 Shaman 0x00 Level(1, Enemy, 1) [10,12] 0x00 0x00 0x0 0x00 [Flux] MoveToAttackAI
UNIT 0x80 AxeKnight 0x00 Level(1, Enemy, 1) [13,14] 0x00 0x00 0x0 0x00 [HandAxe] ScrubBossAI 
UNIT 0x80 LanceKnight 0x00 Level(1, Enemy, 1) [13,13] DropItem 0x00 0x0 0x00 [Javelin,RedGem] ScrubBossAI 
UNIT 0x80 Fighter 0x00 Level(1, Enemy, 1) [6,13] DropItem 0x00 0x0 0x00 [IronAxe] MoveToAttackAI
UNIT 0x80 SwordMercenary 0x00 Level(2, Enemy, 1) [2,9] 0x00 0x00 0x0 0x00 [IronSword,RedGem] MoveToAttackAI
UNIT 0x80 Soldier 0x00 Level(1, Enemy, 1) [12,9] DropItem 0x00 0x0 0x00 [IronLance] WaitToAttackAI
UNIT 0x80 PirateF 0x00 Level(1, Enemy, 1) [9,8] 0x00 0x00 0x0 0x00 [IronAxe] WaitOneTurnMoveAI
UNIT 0x80 Archer 0x00 Level(2, Enemy, 1) [6,6] 0x00 0x00 0x0 0x00 [IronBow] WaitToAttackAI
UNIT 0x80 SwordMercenary 0x00 Level(1, Enemy, 1) [16,7] 0x00 0x00 0x0 0x00 [IronSword] WaitToAttackAI
UNIT 0x80 Shaman 0x00 Level(1, Enemy, 1) [10,6] 0x00 0x00 0x0 0x00 [Flux,Vulnerary] WaitOneTurnMoveAI
UNIT 0x80 Fighter 0x00 Level(1, Enemy, 1) [3,5] 0x00 0x00 0x0 0x00 [IronAxe] WaitToAttackAI 
UNIT 0x80 Pirate 0x00 Level(1, Enemy, 1) [16,12] 0x00 0x00 0x0 0x00 [IronAxe] DestroyVillagesAI
UNIT 0x80 PegasusKnight 0x00 Level(1, Enemy, 1) [13,4] 0x00 0x00 0x0 0x00 [IronLance] WaitToAttackAI
UNIT 0x80 Myrmidon 0x00 Level(2, Enemy, 1) [9,3] 0x00 0x00 0x0 0x00 [IronSword] WaitToAttackAI
UNIT 0x80 SwordMercenary 0x00 Level(3, Enemy, 1) [6,2] 0x00 0x00 0x0 0x00 [Armorslayer,Vulnerary] WaitToAttackAI
UNIT 0x80 Archer 0x00 Level(2, Enemy, 1) [11,2] 0x00 0x00 0x0 0x00 [IronBow] AttackInTwoAI
UNIT 0x80 Pirate 0x00 Level(1, Enemy, 1) [15,1] 0x00 0x00 0x0 0x00 [HandAxe,Vulnerary] ScrubBossAI
UNIT 

BeginningScene:
//LOAD RestrictionType UnitsOffset (for player units)
//ENUN
//LOAD RestrictionType UnitsOffset (for enemy units)
//ENUN
//ENDA
//what for neutral units?
LOAD1 1 Units
ENUN
LOAD1 0 EnemyUnits
ENDA

EndingScene:
MNC2 0x1
ENDA


// Event Labels
// Visiting
UpperLeftVillage:
MUSI
VillageEventItem(Placeholder, HouseBG, Rescue)
MUNO
NoFade
ENDA

UpperLeftHouse:
MUSI
SetBackground(HouseBG)
Text(Placeholder)
EndConvo
TILECHANGE 0x0
MUNO
NoFade
ENDA

// Thief
SpawnThief:
CamON
LOAD1 0x1 ThiefLoader
ENUN
STAL 0x30
CamOFF
NoFade
ENDA

ThiefLoader:
UNIT 0x80 Thief 0x00 Level(1, Enemy, 1) [17,14] DropItem 0x00 0x0 0x00 [IronSword,ChestKey] ThiefTreasureAI
UNIT

// Escape points
EscapePointPointerTable(0x0,Ch1EscapePoints)
Ch1EscapePoints:
EscapePoint(17,14,MoveUp)
EscapePointEnd


